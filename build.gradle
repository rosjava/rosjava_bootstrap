/*
 * Copyright (C) 2013 Google Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not
 * use this file except in compliance with the License. You may obtain a copy of
 * the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
 * License for the specific language governing permissions and limitations under
 * the License.
 */

apply from: project.file('gradle.gradle')

allprojects {
  group="org.ros.rosjava_bootstrap"
  version = "0.3.3"
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'maven'
  apply plugin: 'maven-publish'
  apply plugin: "idea"
  apply plugin: "eclipse"

  sourceCompatibility = 1.6
  targetCompatibility = 1.6

  // These external repositories are copied from bootstrap.gradle.
  repositories {
    jcenter()
    maven {
      url "http://repository.springsource.com/maven/bundles/release"
    }
    maven {
      url "http://repository.springsource.com/maven/bundles/external"
    }
  }

  // Configuration of the deployment repository is copied from the RosPlugin.
  String mavenDeploymentRepository = System.getenv("ROS_MAVEN_DEPLOYMENT_REPOSITORY")
  if (mavenDeploymentRepository != null &&
      mavenDeploymentRepository != "") {
    publishing {
      publications {
        mavenJava(MavenPublication) {
          from components.java
        }
      }
      repositories {
        maven {
           url 'file://' + mavenDeploymentRepository
        }
      }
    }
  } else {
    logger.warn("ROS_MAVEN_DEPLOYMENT_REPOSITORY is not set. Have you sourced setup.bash?")
  }
}

defaultTasks 'publish', 'installDist'
